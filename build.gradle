// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildConfig = [
            'compileSdk': 30,
            'minSdk'    : 21,
            'targetSdk' : 30,

            'version'   : [
                'major': 0,
                'minor': 1,
                'patch': 0,
                'build': 0,
            ],
        ]
        buildConfig.version['name'] = "${buildConfig.version.major}.${buildConfig.version.minor}.${buildConfig.version.patch}"
        buildConfig.version['fullName'] = "${buildConfig.version.name}.${buildConfig.version.build}"
        buildConfig.version['code'] = buildConfig.version.major * 1000000 + buildConfig.version.minor * 10000 + buildConfig.version.patch * 100 + buildConfig.version.build

        versions = [
            'kotlin'       : '1.5.31',
            'junit'        : '4.13.2',
            'appcompat'    : '1.3.1',
            'activity'     : '1.3.1',
            'core_ktx'     : '1.6.0',
            'junit_android': '1.1.3',
            'mockito'      : '3.5.10',
            'mockito_ktx'  : '2.2.0',
            'robolectric'  : '4.5',
            'androidx_test': '1.4.0',
            'fragment_test': '1.3.6',
            'junit_ktx'    : '1.1.3',
            'coroutines'   : '1.5.0',
            'coroutine_tst': '1.3.7',
            'espresso'     : '3.4.0',
            'kakao'        : '3.0.4',
            'fragment'     : '1.3.6',
            'lifecycle'    : '2.3.1',
            'rxjava2'      : '2.2.19',
            'rxkotlin2'    : '2.4.0',
            'rxandroid2'   : '2.1.1',
            'rxrelay2'     : '2.1.1',
            'rxbinding3'   : '3.1.0',
            'material'     : '1.4.0',
            'dagger'       : '2.34.1',
            'constraint'   : '2.1.0',
            'objectbox'    : '2.9.1',
            'swipelayout'  : '1.3.0',
            'gplay_ktx'    : '1.3.7',
            'dynamic_links': '20.1.1',
            'lottie'       : '3.4.1',
            'firebase'     : '19.0.1',
        ]
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "io.objectbox:objectbox-gradle-plugin:${versions.objectbox}"
        classpath "gradle.plugin.com.betomorrow.gradle:appcenter-plugin:1.2.1"
        classpath 'com.google.gms:google-services:4.3.10'
    }
}

plugins {
    id("io.gitlab.arturbosch.detekt").version("1.9.1")
}

detekt {
    failFast = true
    parallel = true
    config = files("config/detekt.yml")
    input = files(projectDir)
    //baseline = file("config/detekt_baseline.xml")

    reports {
        html {
            enabled = true
            destination = file("build/reports/detekt.html")
        }
        xml.enabled = false
        txt.enabled = false
    }
}

tasks.detekt.jvmTarget = "1.8"

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' } // for zerobranch SwipeLayout
    }
}

// Workaround for `android { lint { checkDependencies true ...` which is not working for now.
// Same with `lintOptions`.
subprojects {
    if (it.name == "lint") return

    afterEvaluate {
        android {
            lint {
                abortOnError true
                checkDependencies true
            }
        }
        dependencies {
            lintChecks project(':lint')
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
